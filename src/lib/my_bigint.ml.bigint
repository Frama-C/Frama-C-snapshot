(**************************************************************************)
(*                                                                        *)
(*  This file is part of Frama-C.                                         *)
(*                                                                        *)
(*  Copyright (C) 2007-2011                                               *)
(*    CEA (Commissariat à l'énergie atomique et aux énergies              *)
(*         alternatives)                                                  *)
(*                                                                        *)
(*  you can redistribute it and/or modify it under the terms of the GNU   *)
(*  Lesser General Public License as published by the Free Software       *)
(*  Foundation, version 2.1.                                              *)
(*                                                                        *)
(*  It is distributed in the hope that it will be useful,                 *)
(*  but WITHOUT ANY WARRANTY; without even the implied warranty of        *)
(*  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the         *)
(*  GNU Lesser General Public License for more details.                   *)
(*                                                                        *)
(*  See the GNU Lesser General Public License version 2.1                 *)
(*  for more details (enclosed in the file licenses/LGPLv2.1).            *)
(*                                                                        *)
(**************************************************************************)

type t = Big_int.big_int
module M = struct
let my_int64_of_big_int i = Int64.of_string (Big_int.string_of_big_int i)
let my_big_int_of_int64 i = Big_int.big_int_of_string (Int64.to_string i)
(* Will be used if Big_int.int64_of_big_int does not exist. This is the case
   in OCaml <= 3.10.2. Otherwise the [include Big_int] at the end of this file
   will redefine it to the standard optimal version... *)
let int64_of_big_int = my_int64_of_big_int
let big_int_of_int64 = my_big_int_of_int64

include Big_int
(* ...except these functions are buggy before Ocaml 3.11.1 with 32bits wordsize:
  (<http://caml.inria.fr/mantis/my_view_page.php> PR#4792 and PR#4804)
  So in that case, their standard optimal version is not used. *)
let int64_of_big_int, big_int_of_int64 =
  if (Sys.word_size = 32) && (Sys.ocaml_version < "3.11.1")
  then my_int64_of_big_int, my_big_int_of_int64
  else    int64_of_big_int,    big_int_of_int64

let equal = eq_big_int

let compare = compare_big_int

(* Nb of significant digits in a "word" of Big_int. *)
let nb_digits_of_big_int =
  let r =
    let rec nb_digits y =
      if 1 = num_digits_big_int (power_int_positive_int 2 y)
      then nb_digits (y + 1)
      else y
    in nb_digits 1
  in r

let base = power_int_positive_int 2 nb_digits_of_big_int
let base16bits = power_int_positive_int 2 16


(* If X is such that x
   = let f a x   =(a * base) + x        in List.fold_left f 0 X,
   and Y such that y
   = let f a y   =(a * base) + y        in List.fold_left f 0 Y,
   we have map2_base base op x y =
       let f a x y =(a * base) + (op x y) in List.fold_left f 0 X Y
*)
let map2_base b op x y =
  let rec map2_base_rec a x y =
    let (qx, mx) = quomod_big_int x b
    and (qy, my) = quomod_big_int y b
    in let res_m = op mx my
       and res_q =
        if (eq_big_int zero_big_int qx)
          && (eq_big_int zero_big_int qy)
        then a
        else map2_base_rec a qx qy
    in add_big_int (mult_big_int res_q b) res_m
  in map2_base_rec zero_big_int x y


let bitwise_op_positive_big_int op x y =
  assert (ge_big_int x zero_big_int);
  assert (ge_big_int y zero_big_int);
  let g =
    let f u v = assert(is_int_big_int u) ;
      assert(is_int_big_int v) ;
      let r = op (int_of_big_int u) (int_of_big_int v)
      in big_int_of_int (r)
    in map2_base base16bits f
  in let r = map2_base base g x y
  in assert (ge_big_int r zero_big_int);
    r

let lnot_big_int w = minus_big_int (succ_big_int w)

let shift_left_big_int x y = (* idem multiplication *)
  mult_big_int x (power_int_positive_big_int 2 y)

let shift_right_big_int x y = (* idem division rounding to -oo *)
  div_big_int x (power_int_positive_big_int 2 y)

let power_two =
  let h = Hashtbl.create 7 in
  fun k ->
      try
        Hashtbl.find h k
      with Not_found ->
        let p = power_int_positive_int 2 k in
        Hashtbl.add h k p;
        p

let two_power y =
    try
      let k = int_of_big_int y in
      power_two k
    with Failure _ -> assert false

let log_shift_right_big_int x y = (* no meaning for negative value of x *)
  if (lt_big_int x zero_big_int)
  then raise (Invalid_argument "log_shift_right_big_int")
  else shift_right_big_int x y

let bitwise_op_big_int op x y =
  let (positive_x, op_sx) =
    if gt_big_int zero_big_int x
    then (lnot_big_int x, (fun u v -> op (lnot u) v))
    else (x, op)
  in let (positive_y, op_sx_sy) =
      if gt_big_int zero_big_int y
      then (lnot_big_int y, (fun u v -> op_sx u (lnot v)))
      else (y, op_sx)
  in let (positive_op_map, op_map) =
      if 0 = (op_sx_sy 0 0)
      then (op_sx_sy, (fun w -> w))
      else ((fun u v -> lnot (op_sx_sy u v)), lnot_big_int)
  in op_map (bitwise_op_positive_big_int positive_op_map positive_x positive_y)


let land_big_int = bitwise_op_big_int (land)
let lor_big_int  = bitwise_op_big_int (lor)
let lxor_big_int = bitwise_op_big_int (lxor)

(* Get the value encoded from the 'first' to 'last' bit of 'x' :
   Shift right 'x' and apply a mask on it.
   The result is:  div (mod x (2**(last+1))) (2**first) *)
let bitwise_extraction first_bit last_bit x =
  assert (first_bit <= last_bit);(* first_bit <= last_bit *)
  assert (first_bit >= 0);       (* first_bit >= 0        *)
  let q = div_big_int x (power_int_positive_int 2 first_bit) in
  let r = mod_big_int q (power_int_positive_int 2 (1 + last_bit - first_bit)) in
  r

let minus_one_big_int = minus_big_int unit_big_int
let thirty_two_big_int = big_int_of_int 32


(* To export *)

  let small_nums = Array.init 33 (fun i -> big_int_of_int i)

  let zero = zero_big_int
  let one = unit_big_int
  let minus_one = minus_big_int unit_big_int
  let two = small_nums.(2)
  let four = small_nums.(4)
  let eight = small_nums.(8)
  let thirtytwo = small_nums.(32)
  let onethousand = big_int_of_int 1000
  let billion_one = big_int_of_int 1_000_000_001

  let is_zero v = (sign_big_int v) = 0

  let rem = mod_big_int
  let div = div_big_int
  let mul = mult_big_int

  let sub = sub_big_int

  let abs = abs_big_int
  let succ = succ_big_int
  let pred = pred_big_int
  let neg = minus_big_int

  let add = add_big_int

  let hash c =
    let i =
      try
        int_of_big_int c
      with Failure _ -> int_of_big_int (rem c billion_one)
    in
    197 + i

  let shift_right_logical = log_shift_right_big_int
  let shift_right = shift_right_big_int
  let shift_left = shift_left_big_int

  let logand = land_big_int
  let lognot = lnot_big_int
  let logor = lor_big_int
  let logxor = lxor_big_int

  let le = le_big_int
  let lt = lt_big_int
  let ge = ge_big_int
  let gt = gt_big_int

  let to_int v =
    try int_of_big_int v
    with Failure "int_of_big_int" -> assert false
  let of_int i =
    if 0 <= i && i <= 32
    then small_nums.(i)
    else big_int_of_int i

      (* for the two functions below wait until the minimum supported
         OCaml version is after:
         http://caml.inria.fr/mantis/print_bug_page.php?bug_id=4792
      *)
  let of_int64 i = big_int_of_string (Int64.to_string i)
  let to_int64 i = Int64.of_string (string_of_big_int i)
  let max_int64 = of_int64 Int64.max_int
  let min_int64 = of_int64 Int64.min_int
  let bits_of_max_float = of_int64 (Int64.bits_of_float max_float)
  let bits_of_most_negative_float =
    of_int64 (Int64.bits_of_float (-. max_float))

  let of_string = big_int_of_string
  let to_string = string_of_big_int
  let to_float = float_of_big_int
  let of_float _ = assert false

  let minus_one = pred zero

  let two_power_60 = power_two 60
  let two_power_64 = power_two 64

  let add_2_64 x = add two_power_64 x

  let pretty ?(hexa=false) fmt v =
    let rec aux v =
      if gt v two_power_60 then
        let quo, rem = quomod_big_int v two_power_60 in
        aux quo;
        Format.fprintf fmt "%015LX" (to_int64 rem)
      else
        Format.fprintf fmt "%LX" (to_int64 v)
    in
    if hexa then
      if equal v zero then Format.pp_print_string fmt "0"
      else if gt v zero then (Format.pp_print_string fmt "0x"; aux v)
      else (Format.pp_print_string fmt "-0x"; aux (minus_big_int v))
      else
        Format.pp_print_string fmt (to_string v)

  let is_one v = equal one v
  let pos_div  = div

  let pos_rem = rem
  let native_div = div

  let c_div u v =
    let bad_div = div u v in
    if (lt u zero) && not (is_zero (rem u v))
    then
      if lt v zero
      then pred bad_div
      else succ bad_div
    else bad_div


  let c_rem u v =
    sub u (mul v (c_div u v))

  let cast ~size ~signed ~value =
    let factor = two_power size in
    let mask = two_power (sub size one) in

    if (not signed) then pos_rem value factor
    else
      if equal (logand mask value) zero
    then logand value (pred mask)
    else
      logor (lognot (pred mask)) value

  let two_power = two_power

  let power_two = power_two

  let extract_bits ~start ~stop v =
    assert (ge start zero && ge stop start);
    (*Format.printf "%a[%a..%a]@\n" pretty v pretty start pretty stop;*)
    let r = bitwise_extraction (to_int start) (to_int stop) v in
      (*Format.printf "%a[%a..%a]=%a@\n" pretty v pretty start pretty stop pretty r;*)
      r

  let is_even v = is_zero (logand one v)

  (** [pgcd u 0] is allowed and returns [u] *)
  let pgcd u v =
    let r =
      if is_zero v
      then u
      else gcd_big_int u v in
      r

  let ppcm u v =
    if u = zero || v = zero
    then zero
    else native_div (mul u v) (pgcd u v)

  let length u v = succ (sub v u)

  let min = min_big_int
  let max = max_big_int

  let round_down_to_zero v modu =
    mul (pos_div v modu) modu

  (** [round_up_to_r m r modu] is the smallest number [n] such that
         [n]>=[m] and [n] = [r] modulo [modu] *)
  let round_up_to_r ~min:m ~r ~modu =
    add (add (round_down_to_zero (pred (sub m r)) modu) r) modu

  (** [round_down_to_r m r modu] is the largest number [n] such that
     [n]<=[m] and [n] = [r] modulo [modu] *)
  let round_down_to_r ~max:m ~r ~modu =
    add (round_down_to_zero (sub m r) modu) r

  let to_num = Num.num_of_big_int

  (* only for x >= 0 *)
  let popcount x = 
    let rec aux x acc =
      if is_zero x
      then acc
      else
	let acc = acc + (to_int (logand x one)) in
	aux (shift_right x one) acc
    in
    aux x 0
end

module type S = sig
  val equal : t -> t -> bool
  val compare : t -> t -> int
  val le : t -> t -> bool
  val ge : t -> t -> bool
  val lt : t -> t -> bool
  val gt : t -> t -> bool
  val add : t -> t -> t

  val sub : t -> t -> t

  val mul : t -> t -> t

  val native_div : t -> t -> t
  val rem : t -> t -> t
  val pos_div : t -> t -> t
  val c_div : t -> t -> t

  val c_rem : t -> t -> t

  val cast: size:t -> signed:bool -> value:t -> t
  val abs : t -> t
  val one : t
  val two : t
  val four : t
  val onethousand : t
  val minus_one : t
  val is_zero : t -> bool
  val is_one : t -> bool
  val pgcd : t -> t -> t
  val ppcm : t -> t -> t
  val min : t -> t -> t
  val max : t -> t -> t
  val length : t -> t -> t (** b - a + 1 *)
  val of_int : int -> t
  val of_float : float -> t
  val of_int64 : Int64.t -> t
  val to_int64 : t -> int64
  val to_int : t -> int
  val to_float : t -> float
  val neg : t -> t

  val succ : t -> t
  val pred : t -> t
  val round_up_to_r : min:t -> r:t -> modu:t -> t
  val round_down_to_r : max:t -> r:t -> modu:t -> t
  val pos_rem : t -> t -> t
  val shift_left : t -> t -> t
  val shift_right : t -> t -> t
  val logand : t -> t -> t
  val logor : t -> t -> t
  val logxor : t -> t -> t
  val lognot : t -> t
  val power_two : int -> t
  val two_power : t -> t
  val extract_bits : start:t -> stop:t -> t -> t

  val small_nums : t array
  val zero : t
  val eight : t
  val thirtytwo : t
  val div : t -> t -> t

  val billion_one : t
  val hash : t -> int
  val shift_right_logical : t -> t -> t

  val max_int64 : t
  val min_int64 : t
  val bits_of_max_float : t
  val bits_of_most_negative_float : t
  val of_string : string -> t
  val to_string : t -> string

  val add_2_64 : t -> t
  val is_even : t -> bool
  val round_down_to_zero : t -> t -> t
  val power_int_positive_int: int -> int -> t
  val to_num : t -> Num.num
  val popcount: t -> int
end
include M
